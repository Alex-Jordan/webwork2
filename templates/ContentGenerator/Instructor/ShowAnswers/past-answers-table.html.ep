% use WeBWorK::Utils qw(format_set_name_display);
%
% my $records      = $c->{records};
% my $foundMatches = 0;
%
% for my $studentUser (sort keys %$records) {
	% for my $setName (sort keys %{ $records->{$studentUser} }) {
		% for my $problemNumber (sort { $a <=> $b } keys %{ $records->{$studentUser}{$setName} }) {
			% my @pastAnswerIDs = sort { $a <=> $b } keys %{ $records->{$studentUser}{$setName}{$problemNumber} };
			% my $prettyProblemNumber = $c->{prettyProblemNumbers}{$setName}{$problemNumber};
			<h3>
				<%== maketext('Past Answers for [_1], set [_2], problem [_3]',
					$studentUser, tag('span', dir => 'ltr', format_set_name_display($setName)), $prettyProblemNumber
				) =%>
			</h3>
			<div class="table-responsive">
				<table class="past-answer-table table table-striped" dir="ltr">
					% my $previousTime = -1;
					% for my $answerID (@pastAnswerIDs) {
						% $foundMatches = 1;
						%
						% my %record  = %{ $records->{$studentUser}{$setName}{$problemNumber}{$answerID} };
						% my @answers = @{ $record{answers} };
						% my @scores  = @{ $record{scores} };
						% $previousTime = $record{time} if $previousTime < 0;
						% my $upper_limit = $#scores > $#answers ? $#scores : $#answers;
						%
						<tr <%== $record{time} - $previousTime > $ce->{sessionKeyTimeout}
							? 'class="table-rule"' : '' %>>
							<td style="width:10px"></td>
							<td><small><%= $c->formatDateTime($record{time}) %></small></td>
							% for (my $i = 0; $i <= $upper_limit; $i++) {
								% my $answer     = $answers[$i]             // '';
								% my $answerType = $record{answerTypes}[$i] // '';
								% my $score      = shift(@scores);
								%
								<td style="width:20px"></td>
								% # Color the answer if the user is an instructor, there is an answer,
								% # there is a score, and it is not an essay question.
								<td <%== $isInstructor && $answer ne '' && defined $score && $answerType ne 'essay'
									? $score ? 'style="color:#006600"' : 'style="color:#660000"'
									: '' %> <%== $answerType eq 'essay' ? 'class="essay"' : '' %>>
									% if ($answer eq '') {
										<small><i><%= maketext('empty') %></i></small>
									% } else {
										<%= $answer =%>
									% }
								</td>
							% }
							% if ($record{comment}) {
								<td style="width:20px"></td>
								<td class="comment"><%= maketext('Comment') . ': ' . $record{comment} %></td>
							% }
						</tr>
						% $previousTime = $record{time};
					% }
				</table>
			</div>
		% }
	% }
% }
%
% unless ($foundMatches) {
	<h2><%= maketext('No problems matched the given parameters.') %></h2>
% }
